plugins {
    id 'java-library'
    id 'org.gradle.java.experimental-jigsaw' version '0.1.1'
    id 'maven'
}

sourceCompatibility = 1.9
targetCompatibility = 1.9

description = 'RawHTTP CLI Tool'

javaModule.name = 'com.athaydes.rawhttp.cli'

dependencies {
    compile project(':rawhttp-core')
    testCompile 'junit:junit:4.12'
}

test {
    doFirst {
        jvmArgs += [
                '--add-exports', "com.athaydes.rawhttp.cli/com.athaydes.rawhttp.cli=junit",
        ]
    }
}

task jlink(type: Exec) {
    dependsOn(':rawhttp-core:jar', ':java9-nullable:jar')
    def javaHome = System.properties['java.home']
    def libs = configurations.runtime.collect { it.absolutePath }
    libs += "$javaHome/jmods"
    libs += jar.archivePath.absolutePath

    commandLine 'jlink', '-p', libs.join(File.pathSeparator),
            '--add-modules', 'com.athaydes.rawhttp.cli',
            '--launcher', 'rawhttp=com.athaydes.rawhttp.cli/com.athaydes.rawhttp.cli.Main',
            '--strip-debug', '--compress=2',
            '--no-header-files', '--no-man-pages',
            '--output', 'dist'

    doFirst {
        delete file('dist')
    }
}
