plugins {
    id 'java'
    id 'maven'
    id 'application'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'RawHTTP CLI Tool'

mainClassName = 'com.athaydes.rawhttp.cli.Main'
final runnableJarName = 'rawhttp.jar'

dependencies {
    compile project(':rawhttp-core')
    testCompile 'junit:junit:4.12'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'com.athaydes.rawhttp.cli',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Renato Athaydes',
                'Main-Class': mainClassName
    }

    archiveName = runnableJarName
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

ext.runnableJar = new File(buildDir, 'libs/' + runnableJarName).absolutePath